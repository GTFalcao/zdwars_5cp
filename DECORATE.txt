
// Capture point 'Energy'

actor ControlPointEnergy : inventory { inventory.maxamount 200 }

actor ControlPointEnergyOverheat : CustomInventory {
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	states {
		Spawn:
			TNT1 A -1
			stop
		Pickup:
			TNT1 A 0 A_TakeInventory("ControlPointEnergy", 20)
			TNT1 A 0 A_FireCustomMissile("ControlPointEnergyOverheatMissile",0,0,0,48)
			stop
	}
}

actor ControlPointEnergyOverheatMissile {
	PROJECTILE
	+DONTREFLECT
	+FORCERADIUSDMG
	+FOILINVUL
	reactiontime 1
	states {
		Spawn:
			TNT1 A 1 A_Countdown
			wait
		Death:
			TNT1 A 1 A_Explode(2, 512, 0, 0, 512)
			stop
	}
}

// Open Point Markers

actor ControlPoint0FloatMarker {
	scale 1.5
	height 64
	gravity 0
	+NOBLOCKMAP
	+NOCLIP
	states {
		Spawn:
			P5CP A 1
			P5CP "#" 45 bright ThrustThingZ(0, 90, 0, 0)
			P5CP "#" 5 bright A_Stop
			P5CP "#" 15 bright A_SetGravity(0.5)
			P5CP "#" 5 bright A_SetGravity(0)
			P5CP "#" -1 bright A_ChangeFlag("FLOATBOB", true)
			stop
	}
}
actor ControlPoint1FloatMarker : ControlPoint0FloatMarker {
	states {
		Spawn:
			P5CP B 1
			goto Super::Spawn+1
	}
}
actor ControlPoint2FloatMarker : ControlPoint0FloatMarker {
	states {
		Spawn:
			P5CP C 1
			goto Super::Spawn+1
	}
}
actor ControlPoint3FloatMarker : ControlPoint0FloatMarker {
	states {
		Spawn:
			P5CP D 1
			goto Super::Spawn+1
	}
}
actor ControlPoint4FloatMarker : ControlPoint0FloatMarker {
	states {
		Spawn:
			P5CP E 1
			goto Super::Spawn+1
	}
}

// Macho Man decoration

actor ColosseumMachoMan 20021 {
	+SOLID
	height 69
	states {
		Spawn:
			FIGH A -1
			stop
	}
}

// Powerups

actor ZDWPowerup : CustomInventory {
	inventory.pickupsound "misc/p_pkup"
	inventory.pickupmessage "Special Powerup obtained!"
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
}

// 0 - Speed
actor ZDWPowerupSpeed : ZDWPowerup {
	states {
		Spawn:
			ZDP0 ABCDEFGH 3 bright
			loop
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(203, 0, 0)
			TNT1 A 0 A_GiveInventory("ZDWPowerupGiverSpeed", 1)
			stop
	}
}
actor ZDWPowerupGiverSpeed : PowerupGiver {
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	powerup.duration -45
	powerup.type "Speed"
}

// 1 - Rage
actor ZDWPowerupRage : ZDWPowerup {
	states {
		Spawn:
			ZDP1 ABCDEFGH 6
			loop
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(203, 0, 1)
			TNT1 A 0 A_GiveInventory("ZDWPowerupGiverRage", 1)
			stop
	}
}
actor ZDWPowerupGiverRage : PowerupGiver {
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	powerup.color "60 00 00" 0.25
	powerup.duration -45
	powerup.type "DoubleFiringSpeed"
}

// 2 - Invulnerability
actor ZDWPowerupInvulnerability : ZDWPowerup {
	states {
		Spawn:
			ZDP2 ABCD 6 bright
			loop
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(203, 0, 2)
			TNT1 A 0 A_GiveInventory("ZDWPowerupGiverInvulnerability", 1)
			stop
	}
}
actor ZDWPowerupGiverInvulnerability : PowerupGiver {
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	powerup.color "60 00 00" 0.25
	powerup.duration -25
	powerup.type "Invulnerable"
}

// 3 - Resistance
actor ZDWPowerupResistance : ZDWPowerup {
	states {
		Spawn:
			ZDP3 ABCD 6 Bright
			loop
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(203, 0, 3)
			TNT1 A 0 A_GiveInventory("ZDWPowerupGiverResistance", 1)
			stop
	}
}
actor ZDWPowerupGiverResistance : PowerupGiver {
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	powerup.color GreenMap
	powerup.duration -45
	powerup.type "ZDWResistance"
}
actor PowerZDWResistance : PowerProtection
{
	damagefactor "normal", 0.35
}

// 4 - Mana Regen
/*
actor ZDWPowerupManaRegen : ZDWPowerup {
	states {
		Spawn:
			ZDP4 A -1
			loop
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(203, 0, 4)
			stop
	}
}
*/

// 5 - Mana Pickup
/*
actor ZDWPowerupManaPickup : ZDWPowerup {
	states {
		Spawn:
			ZDP5 A -1
			loop
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(203, 0, 5)
			stop
	}
}
*/